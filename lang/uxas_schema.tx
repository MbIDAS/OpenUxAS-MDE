UxAS_Schema:
    schemas *= SchemaDef
    ;

Comment:
    /\/\/.*$/ |
    /\/\*.*\*\//
    ;

SchemaDef:
    struct_type=ID type=ID? '{'
        fields=FieldsDef?
        xml=XMLDef?
    '}'
    ;

FieldsDef:
    "fields" "{"
        fieldDefs *= FieldDef
    "}"
    ;

FieldDef:
    fieldName=ID "{" fieldAttrs *= FieldDefAttr "}"
    ;

FieldDefAttr:
    attrType="type" ":" type=FieldType ","?|
    attrType="required" ":" required=BOOL ","?|
    attrType="values" ":" "[" values *= FieldDefValue "]" ","?
    ;

FieldType:
    type_name="struct" "{" fieldDefs *= FieldDef "}" is_array="[]"? |
    type_name="struct" struct_name=ID? struct_type=ID? is_array="[]"? |
    type_name=ID is_array="[]"?
    ;

FieldDefValue:
    fieldDefValue=ID ","?
    ;

XMLDef:
    "xml" "{"
        fields *= Field
    "}"
    ;

Field:
    field=TagField |
    field=AttrField |
    field=ChildField |
    field=ChildrenField |
    field=ForeachField |
    field=ValueField |
    field=RenderField
    ;

TagField:
    "tag" ":" tag_value=ValueSpec
    ;

AttrField:
    "attr" attr_type=STRING ":" attr_value=ValueSpec |
    "attr" attr_type=ID ":" attr_value=ValueSpec
    ;

ChildrenField:
    "children" children_source=ID containing_tag=ContainingTag? "{"
        children*=Field
    "}" |
    "children" children_source=ID containing_tag=ContainingTag?"{"
        children*=Field
    "}"
    ;

ContainingTag:
    "tag" containing_tag=ID |
    "tag" containing_tag=STRING
    ;

ChildField:
    "child" tag_name=ID ":" child_value=ValueSpec |
    "child" tag_name=STRING ":" child_value=ValueSpec
    ;

ForeachField:
    "foreach" foreach*=ID "{"
        foreach_fields*=Field
    "}"
    ;

ValueField:
    "value" ":" value=ValueSpec
    ;

RenderField:
    "render" containing_tag=ContainingTag? ":" value=ValueSpec
    ;

ValueSpec:
    value=CurrentValue | value=StringValue | value=ForeachValue | value=NamedValue
    ;

StringValue:
    string_value=STRING
    ;

NamedValue:
    name=ID
    ;

ForeachValue:
    foreach=ID '.' name=ID
    ;

CurrentValue:
    "_"
    ;
