UxASConfig:
    config*=StructValue
    ;

Comment:
    /\/\/.*$/ |
    /\/\*.*\*\//
    ;

Param:
    tag=ID ':' paramValue=ParamValue
    | tag=STRING ':' paramValue=ParamValue
    | include=Include
    | override=OverrideStruct
    | remove=RemoveStruct
    ;

OptComma:
    ','?
    ;

ParamValue:
    value=Include OptComma | value=OverrideStruct OptComma | value=RemoveStruct OptComma | value=StructValue OptComma |
    value=NUMBER OptComma | value=ReferenceValue OptComma | value=ForeachValue OptComma | value=ID OptComma | value=STRING OptComma |  value=ArrayValue OptComma
    ;

ForeachValue:
    foreach=ID '.' param=ID
    ;

ReferenceValue:
    '@' category=ID '[' name=ID ']' '.' param=ID
    ;

ArrayValue:
    '[' values*=ParamValue ']'
    ;

StructValue:
    struct_type=ID type=ID? '{'
        params*=Param
    '}'
    ;

Include:
    "include" include_ref=IncludeRef? filename=STRING
    ;

IncludeRef:
    item_ref=ID '@'
    ;

OverrideStruct:
    "override" type=ID '{'
        params*=Param
    '}'
    ;

RemoveStruct:
    "remove" type=ID
    ;